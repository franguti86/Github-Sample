buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../buildsystem/jacoco.gradle'
apply from: '../buildsystem/findbugs.gradle'
apply from: '../buildsystem/checkstyle.gradle'
apply from: '../buildsystem/utils.gradle'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "net.franguti.githubsampleapp"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
    versionName "1.0.1." + (System.getenv("BUILD_NUMBER") as String ?: "0")
    project.ext.set("archivesBaseName", "GithubSampleApp-" + versionName + "-" + versionCode);
    android.applicationVariants.all {
      variant -> cleanArtifactsFolder(variant)
    }
  }

  signingConfigs {
    debug {
      storeFile file(DEBUG_STORE_FILE)
      storePassword DEBUG_STORE_PASSWORD
      keyAlias DEBUG_KEY_ALIAS
      keyPassword DEBUG_KEY_PASSWORD
    }
    release {
      storeFile file(RELEASE_STORE_FILE)
      storePassword RELEASE_STORE_PASSWORD
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWORD
    }
  }

  buildTypes {

    debug {
      debuggable true
      minifyEnabled false
      signingConfig signingConfigs.debug
      applicationIdSuffix '.debug'
      versionNameSuffix '-DEBUG'
    }

    release {
      signingConfig signingConfigs.release
      debuggable false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {

    envDev {
      applicationId "net.franguti.githubsampleapp.dev"
      resValue "string", "app_name", "Github Sample Dev"
    }

    envStage {
      applicationId "net.franguti.githubsampleapp.stage"
      resValue "string", "app_name", "Github Sample Stage"
    }

    envProd {
      applicationId "net.franguti.githubsampleapp"
      resValue "string", "app_name", "Github Sample"
    }
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:23.1.1'
  compile 'com.android.support:design:23.1.1'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.squareup.dagger:dagger:1.2.2'
  compile 'com.squareup.dagger:dagger-compiler:1.2.2'
  compile 'com.squareup.okhttp:okhttp:2.3.0'
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'org.parceler:parceler-api:1.0.4'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'de.hdodenhof:circleimageview:2.0.0'
  compile 'com.android.support:cardview-v7:23.1.1'
  apt "org.parceler:parceler:1.0.4"
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.0.31-beta'
  testCompile 'com.squareup.okhttp:mockwebserver:2.3.0'
}

check.dependsOn 'checkstyle', 'findBugs', 'lint', 'jacocoReport'



